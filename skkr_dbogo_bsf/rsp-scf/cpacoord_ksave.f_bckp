c Store k- for magnetic bulk
      subroutine cpacoord(
c     ====================
     > itscf,ie,ce,E_Fermi,lmax,nintfc,eta,rightm,bulk,bulkgeo,wrel,
     > kset,xk,wk,nk,intbz,iek,
     > conc,itcpam,cpatol,cpatest,
     > dmata,dmatb,dmatpa,dmatpb,
     > tminvl,tminvr,tminv,tminva,tminvb,
     > tau,taua,taub,gtaua,gtaub,c_light)
c
      implicit real*8 (a-h,o-z)
c
      include '../param.h'
      include 'mpif.h'
      parameter(mdim=mdimdbogo) 
c
      logical wrel,bulk,bulkgeo,sgf,sgfx,sgfy,sgfconv
      logical cpacon,concpa,cpatest,cpalay
c
      character*1 rightm
c
      dimension park(2),xk(mkpar,2),wk(mkpar)
c
      dimension invg(melem),ieqg(melem),ieqgl(melem),ieqgr(melem)
      dimension igordl(melem),igordr(melem)
c
      dimension conc(mintfc)
c
      complex*16 ce,psq,fac,celoc,psqh,fach,celoch
      complex*16 help(dbogomaxp,dbogomaxp),help1(dbogomaxp,dbogomaxp)
      complex*16 taudiag(kmymaxp)
c
      complex*16 tminv(dbogomaxp,dbogomaxp,mintfc)
      complex*16 tminva(dbogomaxp,dbogomaxp,mintfc)
      complex*16 tminvb(dbogomaxp,dbogomaxp,mintfc)
      complex*16 tminvl(dbogomaxp,dbogomaxp,minprc)
      complex*16 tminvr(dbogomaxp,dbogomaxp,minprc)
      complex*16 ttmp(dbogomaxp,dbogomaxp,mintfc,melem)
      complex*16 ttmpl(dbogomaxp,dbogomaxp,minprc,melem)
      complex*16 ttmpr(dbogomaxp,dbogomaxp,minprc,melem)
      complex*16 ctrafo(dbogomaxp,dbogomaxp)
      complex*16 ctrafo2(dbogomaxp,dbogomaxp)
c
      complex*16 xx(mdim,mdim),yy(mdim,mdim),
     > gg2d(bogomaxp,bogomaxp,-minprc:minprc,minprc,0:mprc1+1),
     > gg2dbulk(bogomaxp,bogomaxp,-minprc:minprc,minprc,0:2),
     > gg2de(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:mprc1+1),
     > gg2dbulke(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:2),
     > gg2dh(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:mprc1+1),
     > gg2dbulkh(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:2)
c
      complex*16 xsave(mdim,mdim,mkpar1+1,melem1),
     >           ysave(mdim,mdim,mkpar1+1,melem1),
     > g2de(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:mprc1+1,mkpar1+1),
     > g2dh(lmmaxp,lmmaxp,-minprc:minprc,minprc,0:mprc1+1,mkpar1+1)
c
      complex*16 tau(dbogomaxp,dbogomaxp,mintfc)
      complex*16 taua(dbogomaxp,dbogomaxp,mintfc)
      complex*16 taub(dbogomaxp,dbogomaxp,mintfc)
      complex*16 gtaua(dbogomaxp,dbogomaxp,mintfc)
      complex*16 gtaub(dbogomaxp,dbogomaxp,mintfc)
      complex*16 tau1(dbogomaxp,dbogomaxp,mintfc,melem)
      complex*16 tau2(dbogomaxp,dbogomaxp)
c
      complex*16 dmata(kmymaxp,kmymaxp,mintfc)
      complex*16 dmatpa(kmymaxp,kmymaxp,mintfc)
      complex*16 dmatb(kmymaxp,kmymaxp,mintfc)
      complex*16 dmatpb(kmymaxp,kmymaxp,mintfc)
c
      complex*16 rmat(dbogomaxp,dbogomaxp,melem)
      complex*16 rmatp(dbogomaxp,dbogomaxp,melem)
c
      complex*16 alphalkkr(0:lmaxp,minprc)
      complex*16 alpharkkr(0:lmaxp,minprc)
      complex*16 alphaintkkr(0:lmaxp,mintfc)
      complex*16 alphalkkrh(0:lmaxp,minprc)
      complex*16 alpharkkrh(0:lmaxp,minprc)
      complex*16 alphaintkkrh(0:lmaxp,mintfc)
c
      common/scrpar/alphalkkr,alpharkkr,alphaintkkr
      common/scrparh/alphalkkrh,alpharkkrh,alphaintkkrh
c
      common/test/itest
      common/lay2d/cvec(mtotal,3),nextra,nbulkl,nbulkr,
     &             nprc,ninprc(0:mprc+1)
      common/iterpar/errmax,itermaxl,itermaxr,ichk
c
      common/relfac/fac
      common/relfac/fach
c
      data tol/1.0d-8/ 
      data outtol/1.0d-12/ 
      data tiny/1.0d-6/
c
      save xsave,ysave,g2d
c MPI
      integer root, myrank, nprocs, ierr
      common/mpi/root,myrank,nprocs
      complex*16 taujoin(dbogomaxp,dbogomaxp,mintfc)
c ********************
c initialize constants
c ********************
c
c---> c in rydberg units:
c      c=274.072d0 -- from input
      c=c_light
c---> energy for holes (conjugation is componsated in gstore.f)
      celoch=E_Fermi-conjg(ce)
      psqh=celoch+celoch*celoch/(c*c)
      fach=conjg(psqh)/conjg(celoch)
c---> energy for electrons 
      celoc=E_Fermi+ce
      psq=celoc+celoc*celoc/(c*c)
      fac=psq/celoc
c
      irel=1
c
      nl=lmax+1
      lmmax=nl*nl
      kmymax=2*nl*nl
c
      ninprcl = ninprc(0)
      ninprcr = ninprc(nprc+1)
      ndiml=ninprcl*kmymax*2
      ndimr=ninprcr*kmymax*2
c
      call conjinkmy(ctrafo)
      ctrafo2=ctrafo
      call gjinv(ctrafo2,dbogomaxp,dbogomaxp,detl)
c
      nprc1=nprc
      if(bulkgeo) nprc1=1
      if(nprc1.gt.mprc1) stop ' increase mprc1 !!!'
c
c ****************************************************************
c initialize irreducible representations of point group operations
c ****************************************************************
c
c---> Extended for holes
c     --------------------------------------
      call sphbas(rmat,rmatp,ng,invg,kmymax)
c     --------------------------------------
      if(intbz.eq.0.or.intbz.eq.2) ngeff=1
      if(intbz.eq.1) ngeff=ng
      if(ngeff.gt.melem1) then
        write(6,'(/'' Parameter melem1 is too small!!!'')')
        stop
      end if
c
c
c                        ***************************
c                        * starting CPA iterations *
c                        ***************************
c
      itcpa=1
      concpa=.true.        ! controls CPA tolerance
      cpacon=.true.        ! controls no. of CPA iterations
c
      iekstart=iek
  100 continue
      write(6,*) 'CPA iteration itcpa= ',itcpa
      iek=iekstart
c
c     ----------------------------------------
      call czero(tau,dbogomaxp*dbogomaxp*mintfc)
c     ----------------------------------------
c
c *******************************
c adjust t-matrices for bulk case
c *******************************
c
      if(bulk) then
c The I region is supposed to comprise one PL only !
        if(kset.ge.1) then
c Force L and R t-matrices to be identical with the
c corresponding t-matrices in I
          do li=1,ninprcl
            call repl(tminvl(1,1,li),tminv(1,1,li),2*kmymax,dbogomaxp)
            call repl(tminvr(1,1,li),tminv(1,1,li),2*kmymax,dbogomaxp)
          enddo
c TODO: bnyari,check!! Changed in tcpa_main, interfaces matrices are read
c        else
c This is for only spectral-DOS calculation: force I t-matrices
c to be identical with the corresponding self-consistent L t-matrices
c          do li=1,nintfc
c            call repl(tminv(1,1,li),tminvl(1,1,li),2*kmymax,dbogomaxp)
c          end do
        end if
      end if
c
c *******************************************************************
c transform inverse t-matrices with respect to point group operations
c and find degeneracies
c *******************************************************************
c
c     do li=1,nintfc
c       write(6,*) '  screened tm1 matrix'
c       call outmat1(tminv(1,1,li),kmymax,kmymax,kmymaxp,tol,6)
c     end do
c     ---------------------------------------------------
      call sorttmat(
     > lmax,nintfc,ninprcl,ninprcr,rmat,rmatp,ngeff,invg,
     > tminvl,tminvr,tminv,ttmpl,ttmpr,ttmp,
     > ieqgl,ieqgr,ieqg,igordl,igordr,0)
c     ---------------------------------------------------
c
c ****************************
c loop over k points in 2D IBZ
c ****************************
c
      do ik=1,nk
       write(6,*) 'cpacoord ik= ',ik
       if(modulo(ik-1,nprocs) == myrank) then
        park(1)=xk(ik,1)
        park(2)=xk(ik,2)
c
c *************************************************
c initialize saving of structure constants and SSPO
c *************************************************
c
        iek=iek+1
        if(ik.le.mkpar1) then
          ikuse=ik
        else
          ikuse=mkpar1+1
        end if
c
c    calculate g2d only for the first CPA iteration
        nocalcstr=itcpa-1
c    store g2d only for the first mk1 points
        if(ik.gt.mkpar1) nocalcstr=0
c
        if(bulk) then
c    always calculate SSPO
          sgf=.true.
        else
c    calculate SSPO only for the first scf and CPA iteration
c    or for iek.gt.mek
c         sgf=(itcpa.eq.1.and.itscf.eq.1).or.(ik.gt.mkpar1)
          sgf=(itcpa.eq.1).or.(ik.gt.mkpar1)
        end if
        sgfy=sgf
        sgfx=sgf
c    if vacuum, recalculate R SSPO after every tenth step
        if(rightm.eq.'V')
     >   sgfx=((mod(itscf,10).eq.1).and.(itcpa.eq.1)).or.(ik.gt.mkpar1)
c
c ********************************************
c k-resolved layer-indexed structure constants
c ********************************************
c
        if(nocalcstr.ne.0) then
          call getg2d(g2de(1,1,-minprc,1,0,ikuse),gg2de,
     >                bulkgeo,lmax,nintfc)
          call getg2d(g2dh(1,1,-minprc,1,0,ikuse),gg2dh,
     >                bulkgeo,lmax,nintfc)
c         --------------------------------------------------
          call g2d_pack(gg2d(:,:,:,:,:),gg2de(:,:,:,:,:),
     >                   gg2dh(:,:,:,:,:),lmmax)
c         --------------------------------------------------
        else
c         --------------------------------------------------
          call gstore(park,psq,eta,bulkgeo,'L',lmax,nintfc,
     >                gg2de,.false.,c_light)
          call gstore(park,psqh,eta,bulkgeo,'L',lmax,nintfc,
     >                gg2dh,.true.,c_light)
c         --------------------------------------------------
          call getg2d(gg2de,g2de(1,1,-minprc,1,0,ikuse),
     >                bulkgeo,lmax,nintfc)
          call getg2d(gg2dh,g2dh(1,1,-minprc,1,0,ikuse),
     >                 bulkgeo,lmax,nintfc)
c          -------------------------------------------------
          call g2d_pack(gg2d(:,:,:,:,:),gg2de(:,:,:,:,:),
     >                   gg2dh(:,:,:,:,:),lmmax)
        endif
c
c ********************************
c loop over point group operations
c ********************************
c
        do ig=1,ngeff
          ige=ieqg(ig)
          igel=ieqgl(ig)
          iger=ieqgr(ig)
          ig1=invg(ig)
c
c **********************
c surface Green function
c **********************
c
          if((igel.eq.ig).and.sgfy) then
          if(ichk.gt.0) then
            write(6,'(''L-SGF Point group'',i3,'' K-point'',i5,2f11.5)')
     >         ig,ik,park(1),park(2)
          endif
          if(.not.bulkgeo) then
          call gstore(park,psq,eta,.true.,'L',lmax,nintfc,
     >                gg2dbulke,.false.,c_light)
          call gstore(park,psqh,eta,.true.,'L',lmax,nintfc,
     >                gg2dbulkh,.true.,c_light)
c           ------------------------------------------
          call g2d_pack(gg2dbulk(:,:,:,:,:),gg2dbulke(:,:,:,:,:),
     >                  gg2dbulkh(:,:,:,:,:),lmmax)
c         ------------------------------------------------
          call dugo(lmax,2*kmymax,ttmpl(1,1,1,igel),dbogomaxp,
     >              gg2dbulk(1,1,-minprc,1,0),
     >              gg2dbulk(1,1,-minprc,1,1),
     >              yy,mdim,'L',.true.,irel,sgfconv)
c         ------------------------------------------------
          else
c         ------------------------------------------------
          call dugo(lmax,2*kmymax,ttmpl(1,1,1,igel),dbogomaxp,
     >              gg2d(1,1,-minprc,1,0),
     >              gg2d(1,1,-minprc,1,1),
     >              yy,mdim,'L',.true.,irel,sgfconv)
c         ------------------------------------------------
          end if
          if(.not.sgfconv) then
             write(6,'('' Left SGF'')')
             write(6,'('' k='',2f15.6)') park
             stop
          end if
c
          call repl(ysave(1,1,ikuse,igordl(igel)),yy,
     >              ndiml,mdim)
          else
            call repl(yy,ysave(1,1,ikuse,igordl(igel)),
     >                ndiml,mdim)
          endif
c
          if((iger.eq.ig).and.sgfx) then
          if(ichk.gt.0) then
          write(6,'(''R-SGF Point group'',i3,'' K-point'',i5,2f11.5)')
     >    ig,ik,park(1),park(2)
          endif
          if(.not.bulkgeo) then
          call gstore(park,psq,eta,.true.,'R',lmax,nintfc,
     >                gg2dbulke,.false.,c_light)
          call gstore(park,psqh,eta,.true.,'R',lmax,nintfc,
     >                gg2dbulkh,.true.,c_light)
c           ------------------------------------------
          call g2d_pack(gg2dbulk(:,:,:,:,:),gg2dbulke(:,:,:,:,:),
     >                  gg2dbulkh(:,:,:,:,:),lmmax)
c         ------------------------------------------------
          call dugo(lmax,2*kmymax,ttmpr(1,1,1,iger),dbogomaxp,
     >              gg2dbulk(1,1,-minprc,1,2),
     >              gg2dbulk(1,1,-minprc,1,1),
     >              xx,mdim,'R',.true.,irel,sgfconv)
c         ------------------------------------------------
          else
c         ------------------------------------------------
          call dugo(lmax,2*kmymax,ttmpr(1,1,1,iger),dbogomaxp,
     >              gg2d(1,1,-minprc,1,2),
     >              gg2d(1,1,-minprc,1,1),
     >              xx,mdim,'R',.true.,irel,sgfconv)
c         ------------------------------------------------
          end if
          if(.not.sgfconv) then
             write(6,'('' Right SGF'')')
             write(6,'('' k='',2f15.6)') park
             stop
          end if
c
          call repl(xsave(1,1,ikuse,igordr(iger)),xx,
     >              ndimr,mdim)
          else
          call repl(xx,xsave(1,1,ikuse,igordr(iger)),
     >              ndimr,mdim)
          endif
c
          if(ige.eq.ig) then
c
c ***********************************
c k-resolved tau matrix for interface
c ***********************************
c
c          if(itest.ge.4) then
c          write(6,*) ' YY'
c          do il=1,ninprcl
c          do jl=1,ninprcl
c            i0=(il-1)*(2*kmymax)
c            j0=(jl-1)*(2*kmymax)
c            do i=1,(2*kmymax)
c            do j=1,(2*kmymax)
c              help1(i,j)=yy(i0+i,j0+j)
c            end do
c            end do
c            write(6,*) il,jl
c            call replmsf(help,help1,lmax)
c            call outmat1(help,2*kmymax,2*kmymax,dbogomaxp,outtol,6)
c          end do
c          end do
c          write(6,*) ' XX'
c          do il=1,ninprcr
c          do jl=1,ninprcr
c            i0=(il-1)*(2*kmymax)
c            j0=(jl-1)*(2*kmymax)
c            do i=1,(2*kmymax)
c            do j=1,(2*kmymax)
c              help1(i,j)=xx(i0+i,j0+j)
c            end do
c            end do
c            write(6,*) il,jl
c            call replmsf(help,help1,lmax)
c            call outmat1(help,2*kmymax,2*kmymax,dbogomaxp,outtol,6)
c          end do
c          end do
c          end if
c
          wgeff = wk(ik)/ngeff
c         ----------------------------------------------------
          call tau2d(irel,wgeff,bulkgeo,lmax,2*kmymax,dbogomaxp,nintfc,
     >               ttmp(1,1,1,ig),gg2d,xx,yy,tau1(1,1,1,ig),mdim)
c         ----------------------------------------------------
c
c          do li=1,nintfc
c             if(itest.gt.2) then
c                write(6,*) ' TAU_before BZ sum'
c                call replms(taudiag,tau1(1,1,li,ige),lmax)
c                do k=1,kmymax
c                   write(6,'(i3,2d20.10)') k,taudiag(k)
c                end do
c             end if
c          end do
c
          end if
c
c **************************************
c BZ sum for layer diagonal tau matrices
c **************************************
c
          do li=1,nintfc
c           -----------------------------------------------
            call repl(tau2,tau1(1,1,li,ige),2*kmymax,dbogomaxp)
c           -----------------------------------------------
            call tripmt(rmatp(1,1,ig1),tau2,rmat(1,1,ig1),
     >                  2*kmymax,2*kmymax,dbogomaxp)        
c           -----------------------------------------------
            call addmat(tau(1,1,li),tau2,2*kmymax,dbogomaxp)
c           -----------------------------------------------
          end do
c
        end do
c *********************************
c end of loop over point operations
c *********************************
c
      end if
c MPI if END
      end do
c *************************
c end of loop over k points
c *************************
c
       taujoin = (0.d0,0.d0)
       call mpi_allreduce(tau,taujoin,dbogomaxp*dbogomaxp*mintfc,
     > mpi_double_complex,mpi_sum,mpi_comm_world,ierr)
c
       tau = taujoin
c **********
c CPA solver
c **********
      if (itcpam.gt.0) then 
c     -----------------------------------------------------------------
      call cpacor(conc,tminv,tminva,tminvb,tau,taua,taub,2*kmymax,nintfc,
     >            itcpa,itcpam,cpatol,cpacon,concpa,cpatest,cpaerr)
c     -----------------------------------------------------------------
c
      if(concpa.and.cpacon) goto 100
      if(itest.ge.1) write(6,'('' CPA iterations:'',i3,2x,
     >'' ERROR:'',d15.6)') itcpa-1,cpaerr
      end if 
c
c                        *************************
c                        * ending CPA iterations *
c                        *************************
c
c *************************************************************
c * loop over layers to transform layer diagonal tau matrices *
c * into physical representation and rotate to the local      *
c * frame of reference                                        *
c *************************************************************
c 
      do li=1,nintfc
         cpalay=(1.d0-conc(li)).gt.tiny
c
c         taua(:,:,li)=tau(:,:,li)
c
c        --------------------------------------------------------
         call phystau(taua(1,1,li),tminva(1,1,li),tminva(1,1,li),
     >                alphaintkkr(0,li),alphaintkkrh(0,li),lmax,1)    
         call phystau(tau(1,1,li),tminv(1,1,li),tminv(1,1,li),
     >                alphaintkkr(0,li),alphaintkkrh(0,li),lmax,1)    
c         taua(1:2*kmymax,1:2*kmymax,li)=
c     >        matmul(taua(1:2*kmymax,1:2*kmymax,li),ctrafo)
c         taua(1:2*kmymax,1:2*kmymax,li)=
c     >        matmul(ctrafo2,taua(1:2*kmymax,1:2*kmymax,li))
        
c         if(itest.ge.2) then
c         write(6,'(/'' tau -A '',i2)') li
c         call outmat1(taua(1,1,li),2*kmymax,2*kmymax,dbogomaxp,tol,6)
c         end if
c        --------------------------------------------------------
c
c rotate tau-matrix to local frame of reference if necessary
c
         call repl(gtaua(1,1,li),taua(1,1,li),2*kmymax,dbogomaxp)
c        ------------------------------------------------------
c         if (localmode) then    ! localmode defined in param.h!
c            call tripmt(dmatpa(1,1,li),taua(1,1,li),dmata(1,1,li),
c     >                  2*kmymax,2*kmymax,dbogomaxp)
c         end if
c        ------------------------------------------------------
c
         if(itest.ge.2) then
            write(6,'(/'' tau -A '',i2)') li
c           call replms(taudiag,taua(1,1,li),lmax)
c           do k=1,kmymax
c             write(6,'(i3,2d20.10)') k,taudiag(k)
c           end do
            call outmat1(taua(1,1,li),2*kmymax,2*kmymax,dbogomaxp,tol,6)
         end if
c
         if(cpalay) then
c+------------+
c+ BIG CPA IF +
c+------------+
c
c        --------------------------------------------------------
         call phystau(taub(1,1,li),tminvb(1,1,li),tminvb(1,1,li),
     >                alphaintkkr(0,li),alphaintkkrh(0,li),lmax,1)
c        --------------------------------------------------------
c
c rotate tau-matrix to local frame of reference if necessary
c
         call repl(gtaub(1,1,li),taub(1,1,li),2*kmymax,dbogomaxp)
c        ---------------------------------------------------
c         if (localmode) then    ! localmode defined in param.h!
c            call tripmt(dmatpb(1,1,li),taub(1,1,li),dmatb(1,1,li),
c     >                  kmymax,kmymax,kmymaxp)
c         end if
c        ---------------------------------------------------
c
         if(itest.gt.2) then
            write(6,'(/'' tau -B '',i2)') li
            call outmat1(taub(1,1,li),2*kmymax,2*kmymax,dbogomaxp,tol,6)
         end if
c
         end if
c+----------------+
c+ END BIG CPA IF +
c+----------------+
      end do
c *** end loop over layers ***
c
      return
      end

